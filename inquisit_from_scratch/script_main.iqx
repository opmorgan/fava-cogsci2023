//// Code outline
// Data recording:
// 	  Define what data to record
// Config:
//	  Define user-configured parameters, defaults, and expressions
// Stimuli:
//    practice: 12 target-present, 4 target-absent
//	  main: 32 target-present, 8  target-absent
// Instructions and prompts:
//	  General instructions
//	  response-specific instructions ("/")
//	  response-specific instructions ("z")
// Trials (Define trial types: what stim are presented? Which responses are valid/correct?)
//	  instructions (keys for next, previous)
//    practice target-present ("/")
//	  practice target-absent ("/")
//	  main target-present ("/")
//    main target-absent ("/")
//    practice target-present ("z")
//	  practice target-absent ("z")
//	  main target-present ("z")
//    main target-absent ("z")
// Blocks:
//	  General instructions
//	  response-specific instructions ("/")
//    practice block ("/") (12 t-p, 4 t-a)
//	  main block ("/") (32 t-p, 8 t-a, times 2) 
//	  response-specific instructions ("z")
//    practice block ("z") (12 t-p, 4 t-a)
//	  main block ("z") (32 t-p, 8 t-a, times 2)
// Experiment loop. Define order of blocks

**************************************************************************************************************
	Data recording
**************************************************************************************************************
<monkey>
/ latencydistribution = constant(250)
/ percentcorrect = 95
</monkey>

<data>
/ columns = (build, computer.platform, date, time, values.timestamp, values.time_elapsed_ms, 
subject, group, session, blockcode, blocknum, 
trialcode, trialnum, values.stimulus_left, values.stimulus_right, values.stim_index,
values.target_present, response, correct, latency)
</data>


**************************************************************************************************************
	Config: parameters, defaults, expressions
**************************************************************************************************************

// User-specified variables // TODO: move to config
<parameters>
/ background_color = white
/ foreground_color = black
/ fontfamily_default = "Avenir Next"
/ fontsize_default = 5% // Change to 14pt?
/ nav_button_fontsize_default = 3% // Change to 14pt?
/ fontsize_fixation = 8% // Change to make smaller (e.g., 5%) if using visual fixation cue
/ fontsize_survey = 2% // Change to make smaller (e.g., 5%) if using visual fixation cue
/ fontsize_survey_caption = 3% // Change to make smaller (e.g., 5%) if using visual fixation cue
/ iti = 0 // Time between response and next fixation, or time after timeout with nonresponse
/ fixation_duration = 600
/ shape_duration = 134 // Experiment setting: 134
/ blank_duration = 256
/ mask_duration = 134 // Experiment setting: 134. Set to 2000ms for testing
/ time_limit = 2000 // Maximum response time, after stimulus onset
/ feedback_duration = 500
/ interblock_break_duration = 20000 // Experiment setting: 20000. Set to 1000ms for testing
/ stim_size = 11.12% // Stimulus width, in % screen size
/ stim_xposition_left = 44%
/ stim_xposition_right = 56%
/ stim_yposition = 50%
</parameters>

<defaults>
/ fontstyle = (parameters.fontfamily_default, parameters.fontsize_default)
/ screencolor = parameters.background_color
// txcolor = parameters.foreground_color
/ txbgcolor = parameters.background_color
</defaults>

<expressions>
// Define durations using durations set in <parameters>
/ timeout = parameters.fixation_duration + parameters.time_limit // Maximum response time (stimulus onset + 2000)
// Define event onset times using durations set in <parameters>
/ fixation_onset = 0
/ shape_onset = parameters.fixation_duration
/ blank_onset = parameters.fixation_duration + parameters.shape_duration
/ mask_onset = parameters.fixation_duration + parameters.shape_duration + parameters.blank_duration
/ iti_onset = parameters.fixation_duration + parameters.shape_duration + parameters.blank_duration + parameters.mask_duration

/// Define fixation cue (zoom from big fixation cross)
// fontsize_fixation_big = (parameters.fontsize_fixation * 6)
// fixation_duration_big = (parameters.fixation_duration / 6)
// fixation_onset_big = 0
// fixation_onset_normal = expressions.fixation_duration_big
</expressions>


**************************************************************************************************************
	Initialize values
**************************************************************************************************************
<values>
/ trial_counter = 0 // used in trials to control stimulus selection
/ stim_index = 0 // used in trials to control stimulus selection
/ target_present = "" // used in trials to track whether target is present
/ stimulus = "" // used in trials to record which stimulus was presented
/ stimulus_left = "" // used in trials to record which stimulus was presented
/ stimulus_right = "" // used in trials to record which stimulus was presented
/ time_elapsed_ms = 0 // used to record experiment duration
/ timestamp = 0 // used to record time of events
</values>


**************************************************************************************************************
	Stimuli
**************************************************************************************************************
//// Load stimuli specified in load_stimuli.iqx:
// stim_practice_left; stim_practice_right;
// stim_main_left; stim_main_right
//  Practice:
//    12 target-present
//    4  target-absent
//  Main:
//    32 target-present
// 	  8  target-absent
<include>
/ file = "script_load_stimuli.iqx"
/ file = "script_load_surveys.iqx"
</include>

// Define stimulus selection pools
<list stim_selector_practice>
// Note: controls the selection of the 16 stimuli for the practice condition
// (item list contain the possible itemnumbers)
// The first 12 are target-present; the last 4 are target-absent.
/ items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16)
/ poolsize = 16
/ replace = false
/ resetinterval = 0 // Reset after all items have been selected -- number of items needs to match number presented per block
</list>

<list stim_selector_main>
// Note: controls the selection of the 16 stimuli for the practice condition
// (item list contain the possible itemnumbers)
// The first 32 are target-present; the last 8 are target-absent.
/ items = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31, 32, 33, 34, 35, 36, 37, 38, 39, 40)
/ poolsize = 40
/ replace = false
/ resetinterval = 0 // Reset after all items have been selected -- number of items needs to match number presented per block
</list>

**************************************************************************************************************
	Instructions and prompts
**************************************************************************************************************
// Define instructions and prompts
//	  General 


//	  response-specific instructions ("/")
//	  response-specific instructions ("z")
//	  feedback for correct/incorrect practice trials
<instruct>
/ navigationbuttonfontstyle = (parameters.fontfamily_default, parameters.nav_button_fontsize_default)
/ nextkey = (" ")
/ prevkey = (14) // 14=Backspace
/ finishlabel = "Press SPACE to continue"
/ nextlabel = "Press SPACE to continue"
/ prevlabel = "Press BACKSPACE to go back"
</instruct>

	1=inst_1_welcome;
	2=inst_2_demographics;
	3=inst_3_intro_quickpreview;
	4=inst_4_intro_fixation;

**************************************************************************************************************
<text incorrect_message>
/ items = ("      INCORRECT      ")
/ txcolor = (255, 0, 0)
</text>

<text correct_message>
/ items = ("     CORRECT     ")
/ txcolor = (0, 255, 0)
</text>


// At the beginning, before demographics: 1-2
<htmlpage inst_1_welcome>
/ file = "inst_1_welcome.html"
</htmlpage>

<htmlpage inst_2_demographics>
/ file = "inst_2_demographics.html"
</htmlpage>

// After demographics, before practice1: 3-10
<htmlpage inst_3_quickpreview>
/ file = "inst_3_quickpreview.html"
</htmlpage>

<htmlpage inst_4_intro_fixation>
/ file = "inst_4_intro_fixation.html"
</htmlpage>

<htmlpage inst_5_SLASH_intro_shapes>
/ file = "inst_5_SLASH_intro_shapes.html"
</htmlpage>

<htmlpage inst_5_ZEE_intro_shapes>
/ file = "inst_5_ZEE_intro_shapes.html"
</htmlpage>

<htmlpage inst_6_intro_mask>
/ file = "inst_6_intro_mask.html"
</htmlpage>

<htmlpage inst_7_SLASH_intro_present1>
/ file = "inst_7_SLASH_intro_present1.html"
</htmlpage>

<htmlpage inst_7_ZEE_intro_present1>
/ file = "inst_7_ZEE_intro_present1.html"
</htmlpage>

<htmlpage inst_8_SLASH_intro_present2>
/ file = "inst_8_SLASH_intro_present2.html"
</htmlpage>

<htmlpage inst_8_ZEE_intro_present2>
/ file = "inst_8_ZEE_intro_present2.html"
</htmlpage>

<htmlpage inst_9_intro_absent>
/ file = "inst_9_intro_absent.html"
</htmlpage>

<htmlpage inst_10_SLASH_practice1>
/ file = "inst_10_SLASH_practice1.html"
</htmlpage>

<htmlpage inst_10_ZEE_practice1>
/ file = "inst_10_ZEE_practice1.html"
</htmlpage>

// After practice1, before main1: 11
<htmlpage inst_11_SLASH_main1>
/ file = "inst_11_SLASH_main1.html"
</htmlpage>

<htmlpage inst_11_ZEE_main1>
/ file = "inst_11_ZEE_main1.html"
</htmlpage>

// After main1, before practice 2: interblock break, 12
<page interblock_break>
^^Nice job! You are halfway done—feel free to take a break. Press the spacebar when you are ready to continue.
</page>


<htmlpage inst_12_SLASH_practice2>
/ file = "inst_12_SLASH_practice2.html"
</htmlpage>

<htmlpage inst_12_ZEE_practice2>
/ file = "inst_12_ZEE_practice2.html"
</htmlpage>

// After practice 2, before main 2: 13
<htmlpage inst_13_SLASH_main2>
/ file = "inst_13_SLASH_main2.html"
</htmlpage>

<htmlpage inst_13_ZEE_main2>
/ file = "inst_13_ZEE_main2.html"
</htmlpage>

// After main 2, before end questions: 14
<htmlpage inst_14_end_questions>
/ file = "inst_14_end_questions.html"
</htmlpage>

// At the very end: 15
<htmlpage inst_15_goodbye>
/ file = "inst_15_goodbye.html"
</htmlpage>

**************************************************************************************************************
// Define instruction blocks
<block inst_1_2_UNIVERSAL>
/ preinstructions = (inst_1_welcome, inst_2_demographics)
</block>

<block inst_3_10_SLASH>
/ preinstructions = (
inst_3_quickpreview, 
inst_4_intro_fixation, 
inst_5_SLASH_intro_shapes, 
inst_6_intro_mask, 
inst_7_SLASH_intro_present1, 
inst_8_SLASH_intro_present2,
inst_9_intro_absent,
inst_10_SLASH_practice1,
)
</block>

<block inst_3_10_ZEE>
/ preinstructions = (
inst_3_quickpreview, 
inst_4_intro_fixation, 
inst_5_ZEE_intro_shapes, 
inst_6_intro_mask, 
inst_7_ZEE_intro_present1, 
inst_8_ZEE_intro_present2,
inst_9_intro_absent,
inst_10_ZEE_practice1,
)
</block>

<block inst_11_SLASH_main1>
/ preinstructions = (inst_11_SLASH_main1)
</block>

<block inst_11_ZEE_main1>
/ preinstructions = (inst_11_ZEE_main1)
</block>

<block interblock_break>
/ preinstructions = (interblock_break)
</block>

<block inst_12_SLASH_practice2>
/ preinstructions = (inst_12_SLASH_practice2)
</block>

<block inst_12_ZEE_practice2>
/ preinstructions = (inst_12_ZEE_practice2)
</block>

<block inst_13_SLASH_main2>
/ preinstructions = (inst_13_SLASH_main2)
</block>

<block inst_13_ZEE_main2>
/ preinstructions = (inst_13_ZEE_main2)
</block>

<block inst_14_end_questions>
/ preinstructions = (inst_14_end_questions)
</block>

<block inst_15_goodbye>
/ preinstructions = (inst_15_goodbye)
</block>

// Define placeholder blocks, for testing
<htmlpage placeholder_consent>
/ file = "placeholder_consent.html"
</htmlpage>
<block placeholder_consent>
/ preinstructions = (placeholder_consent)
</block>

<htmlpage placeholder_demographics>
/ file = "placeholder_demographics.html"
</htmlpage>
<block placeholder_demographics>
/ preinstructions = (placeholder_demographics)
</block>

<htmlpage placeholder_ehi_taskexperience_feedback>
/ file = "placeholder_ehi_taskexperience_feedback.html"
</htmlpage>
<block placeholder_ehi_taskexperience_feedback>
/ preinstructions = (placeholder_ehi_taskexperience_feedback)
</block>

<htmlpage placeholder_practice>
/ file = "placeholder_practice.html"
</htmlpage>
<block placeholder_practice>
/ preinstructions = (placeholder_practice)
</block>

<htmlpage placeholder_main>
/ file = "placeholder_main.html"
</htmlpage>
<block placeholder_main>
/ preinstructions = (placeholder_main)
</block>


<block test_block_SLASH>
/ preinstructions = (
inst_1_welcome, 
placeholder_consent,
inst_2_demographics,
placeholder_demographics,
inst_3_quickpreview, 
inst_4_intro_fixation, 
inst_5_SLASH_intro_shapes, 
inst_6_intro_mask, 
inst_7_SLASH_intro_present1, 
inst_8_SLASH_intro_present2,
inst_9_intro_absent,
inst_10_SLASH_practice1,
placeholder_practice,
inst_11_SLASH_main1,
placeholder_main,
interblock_break,
inst_12_SLASH_practice2,
placeholder_practice,
inst_13_SLASH_main2,
placeholder_main,
inst_14_end_questions,
placeholder_ehi_taskexperience_feedback,
inst_15_goodbye,
)
</block>

<block test_block_ZEE>
/ preinstructions = (
inst_1_welcome, 
placeholder_consent,
inst_2_demographics,
placeholder_demographics,
inst_3_quickpreview, 
inst_4_intro_fixation, 
inst_5_ZEE_intro_shapes, 
inst_6_intro_mask, 
inst_7_ZEE_intro_present1, 
inst_8_ZEE_intro_present2,
inst_9_intro_absent,
inst_10_ZEE_practice1,
placeholder_practice,
inst_11_ZEE_main1,
placeholder_main,
interblock_break,
inst_12_ZEE_practice2,
placeholder_practice,
inst_13_ZEE_main2,
placeholder_main,
inst_14_end_questions,
placeholder_ehi_taskexperience_feedback,
inst_15_goodbye,
)
</block>




**************************************************************************************************************
	Trials
**************************************************************************************************************
// Define trial types (what stim are presented? Which responses are valid/correct?)
//	  instructions (keys for next, previous)
//    practice  ("/") 12 target-present, 4 target-absent [16 trials total]
//	  main      ("/") 32 target-present, 8 target-absent [40 trials total]
//    practice  ("z") 12 target-present, 4 target-absent [16 trials total]
//	  main      ("z") 32 target-present, 8 target-absent [40 trials total]

<trial practice_slash>
/ ontrialbegin = [
	values.trial_counter += 1; // is this used?
	values.stim_index = list.stim_selector_practice.nextvalue;
	if (values.stim_index <= 12){ // Trials 1-12: present
		values.target_present = "yes";
	} else if (values.stim_index > 12) { //Trials 13-16: absent
		values.target_present = "no";
	} else {
		values.target_present = "error: stimulus index out of bounds";
	};
	
	//trial.practice_slash.insertstimulustime(clearscreen, expressions.fixation_onset_normal); // 600ms/n (after cue): Refresh screen
	//trial.practice_slash.insertstimulustime(text.stim_fixation, expressions.fixation_onset_normal); // 600ms/n (after cue): Show normal fixation cross after cue

	//trial.practice_slash.insertstimulustime(clearscreen, expressions.shape_onset); // 600ms: refresh screen
	trial.practice_slash.insertstimulustime(picture.stim_practice_left, expressions.shape_onset); // 600ms: show stimulus
	trial.practice_slash.insertstimulustime(picture.stim_practice_right, expressions.shape_onset); // 600ms: show stimulus

	
	trial.practice_slash.insertstimulustime(clearscreen, expressions.blank_onset); // 600+134ms: show blank
	
	trial.practice_slash.insertstimulustime(picture.stim_mask_left, expressions.mask_onset); // 600+134ms+256ms: show mask
	trial.practice_slash.insertstimulustime(picture.stim_mask_right, expressions.mask_onset); // 600+134ms+256ms: show mask

    trial.practice_slash.insertstimulustime(clearscreen, expressions.iti_onset); // 600+134ms+256ms: show blank (iti)
]

// stimulustimes = [0 = stim_fixation_big]
/ stimulustimes = [0 = stim_fixation]

/ beginresponsetime = parameters.fixation_duration
/ responseinterrupt = immediate
/ timeout = expressions.timeout

/ validresponse = ("/", noresponse)
/ iscorrectresponse = [(values.stim_index <= 12 && trial.practice_slash.responsetext == "/") || (values.stim_index > 12 && trial.practice_slash.responsetext != "/")] 

/ ontrialend = [
	trial.practice_slash.resetstimulusframes();
	values.stimulus_left = picture.stim_practice_left.currentitem;
	values.stimulus_right = picture.stim_practice_right.currentitem;
	values.time_elapsed_ms = script.elapsedtime;
	values.timestamp = script.currenttime;
]

/ posttrialpause = parameters.iti
/ recorddata = true
</trial>

<trial practice_z>
/ ontrialbegin = [
	values.trial_counter += 1; // is this used?
	values.stim_index = list.stim_selector_practice.nextvalue;
	if (values.stim_index <= 12){ // Trials 1-12: present
		values.target_present = "yes";
	} else if (values.stim_index > 12) { //Trials 13-16: absent
		values.target_present = "no";
	} else {
		values.target_present = "error: stimulus index out of bounds";
	};
	
	//trial.practice_z.insertstimulustime(clearscreen, expressions.fixation_onset_normal); // 600ms/n (after cue): Refresh screen
	//trial.practice_z.insertstimulustime(text.stim_fixation, expressions.fixation_onset_normal); // 600ms/n (after cue): Show normal fixation cross after cue

	//trial.practice_z.insertstimulustime(clearscreen, expressions.shape_onset); // 600ms: refresh screen
	trial.practice_z.insertstimulustime(picture.stim_practice_left, expressions.shape_onset); // 600ms: show stimulus
	trial.practice_z.insertstimulustime(picture.stim_practice_right, expressions.shape_onset); // 600ms: show stimulus

	
	trial.practice_z.insertstimulustime(clearscreen, expressions.blank_onset); // 600+134ms: show blank
	
	trial.practice_z.insertstimulustime(picture.stim_mask_left, expressions.mask_onset); // 600+134ms+256ms: show mask
	trial.practice_z.insertstimulustime(picture.stim_mask_right, expressions.mask_onset); // 600+134ms+256ms: show mask

    trial.practice_z.insertstimulustime(clearscreen, expressions.iti_onset); // 600+134ms+256ms: show blank (iti)
]

// stimulustimes = [0 = stim_fixation_big]
/ stimulustimes = [0 = stim_fixation]

/ beginresponsetime = parameters.fixation_duration
/ responseinterrupt = immediate
/ timeout = expressions.timeout

/ validresponse = ("z", noresponse)
/ iscorrectresponse = [(values.stim_index <= 12 && trial.practice_z.responsetext == "z") || (values.stim_index > 12 && trial.practice_z.responsetext != "z")] 
// Hack: set correct response for absent trials to responsetext != "z"

/ ontrialend = [
	trial.practice_z.resetstimulusframes();
	values.stimulus_left = picture.stim_practice_left.currentitem;
	values.stimulus_right = picture.stim_practice_right.currentitem;
	values.time_elapsed_ms = script.elapsedtime;
	values.timestamp = script.currenttime;
]

/ posttrialpause = parameters.iti
/ recorddata = true
</trial>

<trial main_slash>
/ ontrialbegin = [
	values.trial_counter += 1;
	values.stim_index = list.stim_selector_main.nextvalue;
	if (values.stim_index <= 32){ // Trials 1-32: present
		values.target_present = "yes";
	} else if (values.stim_index > 32) { //Trials 33-40: absent
		values.target_present = "no";
	} else {
		values.target_present = "error: stimulus index out of bounds";
	};
	
	//trial.main_slash.insertstimulustime(clearscreen, expressions.fixation_onset_normal); // 600ms/n (after cue): Refresh screen
	//trial.main_slash.insertstimulustime(text.stim_fixation, expressions.fixation_onset_normal); // 600ms/n (after cue): Show normal fixation cross after cue
	
	//trial.main_slash.insertstimulustime(clearscreen, expressions.shape_onset); // 600ms: refresh screen
	trial.main_slash.insertstimulustime(picture.stim_main_left, expressions.shape_onset); // 600ms: show stimulus
	trial.main_slash.insertstimulustime(picture.stim_main_right, expressions.shape_onset); // 600ms: show stimulus
	
	trial.main_slash.insertstimulustime(clearscreen, expressions.blank_onset); // 600+134ms: show blank
	
	trial.main_slash.insertstimulustime(picture.stim_mask_left, expressions.mask_onset); // 600+134ms+256ms: show mask
	trial.main_slash.insertstimulustime(picture.stim_mask_right, expressions.mask_onset); // 600+134ms+256ms: show mask

    trial.main_slash.insertstimulustime(clearscreen, expressions.iti_onset); // 600+134ms+256ms: show blank (iti)
]

// stimulustimes = [0 = stim_fixation_big]
/ stimulustimes = [0 = stim_fixation]

/ beginresponsetime = parameters.fixation_duration
/ responseinterrupt = immediate
/ timeout = expressions.timeout

/ validresponse = ("/", noresponse)
/ iscorrectresponse = [(values.stim_index <= 32 && trial.main_slash.responsetext == "/") || (values.stim_index > 32 && trial.main_slash.responsetext != "/")] 
// Hack: set correct response for absent trials to responsetext != "/"
/ ontrialend = [
	trial.main_slash.resetstimulusframes();
	values.stimulus_left = picture.stim_main_left.currentitem;
	values.stimulus_right = picture.stim_main_right.currentitem;
	values.time_elapsed_ms = script.elapsedtime;
	values.timestamp = script.currenttime;
]

/ posttrialpause = parameters.iti
/ recorddata = true
</trial>

<trial main_z>
/ ontrialbegin = [
	values.trial_counter += 1;
	values.stim_index = list.stim_selector_main.nextvalue;
	if (values.stim_index <= 32){ // Trials 1-32: present
		values.target_present = "yes";
	} else if (values.stim_index > 32) { //Trials 33-40: absent
		values.target_present = "no";
	} else {
		values.target_present = "error: stimulus index out of bounds";
	};
	
	//trial.main_z.insertstimulustime(clearscreen, expressions.fixation_onset_normal); // 600ms/n (after cue): Refresh screen
	//trial.main_z.insertstimulustime(text.stim_fixation, expressions.fixation_onset_normal); // 600ms/n (after cue): Show normal fixation cross after cue
	
	//trial.main_z.insertstimulustime(clearscreen, expressions.shape_onset); // 600ms: refresh screen
	trial.main_z.insertstimulustime(picture.stim_main_left, expressions.shape_onset); // 600ms: show stimulus
	trial.main_z.insertstimulustime(picture.stim_main_right, expressions.shape_onset); // 600ms: show stimulus
	
	trial.main_z.insertstimulustime(clearscreen, expressions.blank_onset); // 600+134ms: show blank
	
	trial.main_z.insertstimulustime(picture.stim_mask_left, expressions.mask_onset); // 600+134ms+256ms: show mask
	trial.main_z.insertstimulustime(picture.stim_mask_right, expressions.mask_onset); // 600+134ms+256ms: show mask

    trial.main_z.insertstimulustime(clearscreen, expressions.iti_onset); // 600+134ms+256ms: show blank (iti)
]

// stimulustimes = [0 = stim_fixation_big]
/ stimulustimes = [0 = stim_fixation]

/ beginresponsetime = parameters.fixation_duration
/ responseinterrupt = immediate
/ timeout = expressions.timeout

/ validresponse = ("z", noresponse)
/ iscorrectresponse = [(values.stim_index <= 32 && trial.main_z.responsetext == "z") || (values.stim_index > 32 && trial.main_z.responsetext != "z")] 

/ ontrialend = [
	trial.main_z.resetstimulusframes();
	values.stimulus_left = picture.stim_main_left.currentitem;
	values.stimulus_right = picture.stim_main_right.currentitem;
	values.time_elapsed_ms = script.elapsedtime;
	values.timestamp = script.currenttime;
]
</trial>



**************************************************************************************************************
	Blocks
**************************************************************************************************************
// Define blocks
//    practice block ("/") 12 target-present, 4 target-absent          [16 trials total]
//	  main block     ("/") 32 target-present, 8 target-absent, times 2 [80 trials total]
//    practice block ("z") 12 target-present, 4 target-absent          [16 trials total]
//	  main block     ("z") 32 target-present, 8 target-absent, times 2 [80 trials total]
<block practice_slash>
/ trials = [
	// How to present 12 present, 4 absent?
	// (a) Select without replacement on list of 12 present; list of 4 absent (does this work?)
	// 1-16 = noreplace(practice_present_slash, practice_absent_slash) // Make sure this randomly selects from full pool; if not, move logic.
	// (b) Predefine set of 16 stim, in one trial type. Randomly present those 16.
	1-16 = noreplace(practice_slash)
    // It might be harder to handle responses in (b).
	]
/ errormessage = (incorrect_message, parameters.feedback_duration)
/ correctmessage = (correct_message, parameters.feedback_duration)
</block>

<block practice_z>
/ trials = [
	1-16 = noreplace(practice_z)
	]
/ errormessage = (incorrect_message, parameters.feedback_duration)
/ correctmessage = (correct_message, parameters.feedback_duration)
</block>

<block main_slash>
/ trials = [
	// How to present 32 present, 8 absent?
	// (a) Select without replacement on list of 32 present; list of 8 absent (does this work?)
	// 1-40 = noreplace(main_present_slash, main_absent_slash);
	// 41-80 = noreplace(main_present_slash, main_absent_slash) 
	// (b) Predefine set of 40 stim, in one trial type. Randomly present those 40.
	// It might be harder to handle responses in (b).
	 1-40 = noreplace(main_slash);
	41-80 = noreplace(main_slash);
	]
</block>

<block main_z>
/ trials = [
	 1-40 = noreplace(main_z);
	41-80 = noreplace(main_z);
	]
</block>


**************************************************************************************************************
	Blocks
**************************************************************************************************************
// Define experiment
// Order:
//	  Demographics
//	  General instructions
//	  response-specific instructions ("/")
//    practice block ("/") (12 t-p, 4 t-a)
//	  main block ("/") (32 t-p, 8 t-a, times 2) 
//	  response-specific instructions ("z")
//    practice block ("z") (12 t-p, 4 t-a)
//	  main block ("z") (32 t-p, 8 t-a, times 2)
//	  Handedness questionnaire; open-ended feedback form
// For 1/2 subjects, do "z" blocks first.
<expt>
/ subjects = (1 of 2) // Subjects who will respond with "/" first
// To automatically assign subject ID and group by odd/even, host on inquisit website.
// https://www.millisecond.com/support/docs/current/html/articles/websubjectnumbers.htm
/ blocks = [
	1=inst_1_2_UNIVERSAL;
	2=placeholder_demographics;
	3=inst_3_10_SLASH;
	4=practice_slash;
	5=inst_11_SLASH_main1;
	6=main_slash;
	7=inst_12_SLASH_practice2;
	8=practice_z;
	9=inst_13_SLASH_main2;
	10=main_z;
	11=inst_14_end_questions;
	12=ehi_short;
	13=end_questions;
	14=inst_15_goodbye;
	]
</expt>

<expt>
/ subjects = (2 of 2) // Subjects who will respond with "z" first
/ blocks = [
	1=inst_1_2_UNIVERSAL;
	2=demographics;
	3=inst_3_10_ZEE;
	4=practice_z;
	5=inst_11_ZEE_main1;
	6=main_z;
	7=inst_12_ZEE_practice2;
	8=practice_slash;
	9=inst_13_ZEE_main2;
	10=main_slash;
	11=inst_14_end_questions;
	12=ehi_short;
	13=end_questions;
	14=inst_15_goodbye;
	]
</expt>
